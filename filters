#!/usr/bin/env perl

# Written in 2023 by Mason Loring Bliss <mason@blisses.org>
#
# To the extent possible under law, the author(s) have dedicated all
# copyright and related and neighboring rights to this software to the public
# domain worldwide. This software is distributed without any warranty.
#
# You should have received a copy of the CC0 Public Domain Dedication along
# with this software. If not, see:
#
#   http://creativecommons.org/publicdomain/zero/1.0/

use warnings;
use strict;

open my $filterhead,'<',"$ENV{HOME}/filter.head"
    or die "Couldn't open filter.head $!";
open my $filterlist,'<',"$ENV{HOME}/filter.list"
    or die "Couldn't open filter.list: $!";
open my $procmailrc,'>',"$ENV{HOME}/.procmailrc"
    or die "Couldn't open .procmailrc: $!";

while (<$filterhead>) {
    print $procmailrc $_;
}

while (<$filterlist>) {
    chomp;
    my ($method, $mailbox) = split;
    my $listname = $mailbox;
    my $rule = ":0:\n";
    $listname =~ s/-bounces//;
    $listname =~ s/\./_/g;

    if ($method eq "sender") {
        $rule .= "*^Sender:.*$mailbox\n";
    } elsif ($method eq "xloop") {
        $rule .= "*^X-Loop:.*$mailbox\n";
    } elsif ($method eq "listid") {
        $rule .= "*^List-ID:.*$mailbox\n";
    } elsif ($method eq "rss") {
        $rule .= "*^X-RSS-Feed:.*$mailbox\n";
    } elsif ($method eq "to") {
        $rule .= "*^To:.*$mailbox\n";
    } elsif ($method eq "from") {
        $rule .= "*^From:.*$mailbox\n";
    } elsif ($method eq "deliveredto") {
        $rule .= "*^Delivered-To:.*$mailbox\n";
    } else {
        next;
    }
    print $procmailrc "$rule$listname/\n\n";

    if (! -d "$ENV{HOME}/mail/$listname") {
        print "Creating $ENV{HOME}/mail/$listname\n";
        system "maildirmake.dovecot $ENV{HOME}/mail/$listname";
    }
}
close $procmailrc;
close $filterlist;
